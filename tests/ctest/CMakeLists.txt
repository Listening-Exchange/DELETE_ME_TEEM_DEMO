# tests/ctest/CMakeLists.txt: Test driver for Teem's CTest-based tests
# Copyright (C) 2025  University of Chicago
# See ../../LICENSE.txt for licensing terms

# Note: these tests are very incomplete. It is unclear how much of Teem's
# future development will hinge on CTest-based testing, now that the
# Python/CFFI bindings offer a more convenient way to call into Teem.  But they
# do test something useful, and now for the Teem v2 re-write they work again.

# To run tests (from top-level):
#
#    # for running tests locally
#    cmake -S . -B <builddir> -DBUILD_TESTING=ON
#    cmake --build <builddir>
#    cd <builddir>
#    ctest      # run all tests
#    ctest -V   # verbose
#
#    # for submitting dashboard
#    cd <builddir>
#    ctest -D Nightly    # or Experimental, Continuous

# HEY: how to do coverage tests?

###-------------------------------------------------------------------------------------
# Basic set-up

message(STATUS "Setting up testing in tests/ctest/")

# Make sure tests can see headers generated by the build system
include_directories(${CMAKE_BINARY_DIR}/include)

# Create a small *static* "testutil" library from testutil.{c,h}
add_library(teem_testutil STATIC testutil.c)
target_include_directories(teem_testutil PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
target_link_libraries(teem_testutil PRIVATE Teem)

###-------------------------------------------------------------------------------------
# Two helper functions for creating and describing tests:
# teem_create_test_bin: creates the executable test program
# teem_add_test: uses the executable to declare a new test

# teem_create_test_bin(<target> <src> [USE_TMPDIR]):
# From test program source file <src> create executable, hereafter referred to as
# <target>.  If USE_TMPDIR, then create a tmp directory "<target>_tmp" that the test can
# use for writing files.  Attach the necessary environment variables to the target, to be
# re-used by teem_add_test. It makes more sense to "attach" the tmp dir to the target
# than to the test, because needing and using tmp dir is more directly a consequence of
# the source code of the test program, than the circumstances of how the test executable
# is run.
#
function(teem_create_test_bin target src)
  set(options USE_TMPDIR)
  # this sets TCTE_USE_TMPDIR if "USE_TMPDIR" was passed
  cmake_parse_arguments(TCTE "${options}" "" "" ${ARGN})

  add_executable(${target} ${src})
  target_link_libraries(${target} PRIVATE Teem teem_testutil)

  # Always store the data dir for later
  set_property(TARGET ${target} PROPERTY TEEM_TEST_DATA_DIR "${CMAKE_SOURCE_DIR}/data")

  # Optionally create a tmpdir and remember it
  if(TCTE_USE_TMPDIR)
    set(tmpdir "${CMAKE_CURRENT_BINARY_DIR}/${target}_tmp")
    file(MAKE_DIRECTORY "${tmpdir}")
    set_property(TARGET ${target} PROPERTY TEEM_TEST_TMP_DIR "${tmpdir}")
  endif()
endfunction()

# teem_add_test(<target> [NAME <name>] [ARGS <args>...])
# This creates the CTest test via add_test() around the <target> created by
# teem_create_test_bin above.  The NAME option, if used, creates a name for the
# test different than ${target} The ARGS option says how to run it on the command-line.
#
function(teem_add_test target)
  set(options) # no boolean options to parse
  set(oneValueArgs NAME)
  set(multiValueArgs ARGS)
  # this sets TAT_NAME to argument after "NAME" (if any),
  # and TAT_ARGS to list of args after "ARGS" (if any).
  cmake_parse_arguments(TAT "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  if(NOT TARGET ${target})
    message(FATAL_ERROR "teem_add_test: target ${target} not found")
  endif()

  # Compute test name
  if(TAT_NAME)
    set(test_name "${TAT_NAME}")
  else()
    set(test_name "${target}")
  endif()

  # Get stored metadata from the target
  get_property(data_dir TARGET ${target} PROPERTY TEEM_TEST_DATA_DIR)
  get_property(tmp_dir  TARGET ${target} PROPERTY TEEM_TEST_TMP_DIR)

  # Build up environment string
  set(env_list "TEEM_TEST_DATA_DIR=${data_dir}")
  if(tmp_dir)
    list(APPEND env_list "TEEM_TEST_TMP_DIR=${tmp_dir}")
  endif()

  # Register the test
  add_test(NAME ${test_name} COMMAND ${target} ${TAT_ARGS})
  set_tests_properties(${test_name} PROPERTIES
    ENVIRONMENT "${env_list}"
  )
endfunction()

###-------------------------------------------------------------------------------------
# The Tests (such as they are)
#
# These are the component libraries that have tests; more added as time permits
# TEEM_LIB_LIST except that most (those libs yearning for tests) are commented out
add_subdirectory(air)
# add_subdirectory(hest)
add_subdirectory(biff)
add_subdirectory(nrrd)
# add_subdirectory(ell)
# add_subdirectory(moss)
add_subdirectory(unrrdu)
# add_subdirectory(alan)
# add_subdirectory(tijk)
add_subdirectory(gage)
# add_subdirectory(dye)
# add_subdirectory(bane)
# add_subdirectory(limn)
# add_subdirectory(echo)
# add_subdirectory(hoover)
# add_subdirectory(seek)
add_subdirectory(ten)
# add_subdirectory(elf)
# add_subdirectory(pull)
# add_subdirectory(coil)
# add_subdirectory(push)
# add_subdirectory(mite)
add_subdirectory(meet)

# Define a helper for Teem command-line tools
function(teem_add_executable name)
  add_executable(${name} ${ARGN})
  target_link_libraries(${name} PRIVATE Teem)

  install(TARGETS ${name}
    RUNTIME DESTINATION ${BIN_INSTALL_DIR}
    COMPONENT Runtime
  )
endfunction()

# List of all Teem command-line tools.  TeemV2 stripped down the number of tools built;
# those no longer built are commented out below, with explanation.  TeemV2 also dropped
# the increasingly annoying BUILD_EXPERIMENTAL_LIBS and BUILD_EXPERIMENTAL_APPS toggles
set(Teem_EXECUTABLES
  # airSanity: all its tests are included in nrrdSanity
  # nrrdSanity: no needed since unu starts by calling nrrdSanityOrDie
  #     and the other build info it provided is now produced by the new "unu built"
  # cubic: just calls ell_cubic; now more easily done via Python wrappers
  # qbert: created for volume rendering on GPUs from 20 years ago
  # ilk: now available as "unu ilk"
  # emap, deconv, gkms, spots: functionality not currently handled elsewhere, but
  #     probably not actually used by anyone?
  #     If you disagree say so: https://discord.gg/xBBqZGXkF7
  #     Also, these are still available via GNUmakefiles
  # talkweb: the web has changed in 20 years
  # pprobe: functionality available via "gprobe -pp"
  # ungantry: functionality available via "gprobe -pg" and an appropriate grid
  # ninspect: moved to "unu ninspect"
  overrgb
  mrender
  miter
  vprobe
  gprobe
  unu
  puller
  tend
)

# Define each executable
foreach(exec IN LISTS Teem_EXECUTABLES)
  teem_add_executable(${exec} ${exec}.c)
endforeach()

# Collect source files for IDE grouping
set(_Teem_EXEC_SOURCES)
foreach(exec IN LISTS Teem_EXECUTABLES)
  list(APPEND _Teem_EXEC_SOURCES "${CMAKE_CURRENT_SOURCE_DIR}/${exec}.c")
endforeach()

# IDE grouping: use TREE so folder structure is preserved
# and PREFIX "Teem/src/bin" so all executables appear under a single top-level folder,
# alongside the other component libraries
source_group(
  TREE "${CMAKE_CURRENT_SOURCE_DIR}"
  PREFIX "Teem/src/bin"
  FILES ${_Teem_EXEC_SOURCES}
)
